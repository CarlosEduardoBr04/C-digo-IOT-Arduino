#include <SPI.h>
#include <MFRC522.h>
#include <LiquidCrystal.h>

// Configuração dos pinos
#define SS_PIN 10
#define RST_PIN 9
MFRC522 mfrc522(SS_PIN, RST_PIN);

LiquidCrystal lcd(6, 7, 5, 4, 3, 2);

// Chave de autenticação MIFARE
MFRC522::MIFARE_Key key;

void setup() {
  Serial.begin(9600);
  SPI.begin();
  mfrc522.PCD_Init();
  lcd.begin(16, 2);
  mensagemInicial();
  
  // Inicialização da chave com valores padrão
  for (byte i = 0; i < 6; i++) key.keyByte[i] = 0xFF;
}

void loop() {
  while (!mfrc522.PICC_IsNewCardPresent()) {
    delay(100);
  }
  
  if (mfrc522.PICC_ReadCardSerial()) {
    modoLeitura();
  }
}

void mensagemInicial() {
  Serial.println("Aproxime o seu cartão do leitor...");
  lcd.clear();
  lcd.print("Aproxime o seu");
  lcd.setCursor(0, 1);
  lcd.print("cartão do leitor");
}

void modoLeitura() {
  Serial.print("UID da tag : ");
  String conteudo = "";
  for (byte i = 0; i < mfrc522.uid.size; i++) {
    Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
    Serial.print(mfrc522.uid.uidByte[i], HEX);
    conteudo.concat(String(mfrc522.uid.uidByte[i]<0x10 ? " 0" : " "));
    conteudo.concat(String(mfrc522.uid.uidByte[i], HEX));
  }
  Serial.println();
  
  // Autenticação e leitura do setor 1, bloco 4
  byte setor = 1;
  byte blocoAddr = 4;
  byte trailerBlock = 7;
  byte status;
  byte buffer[18];
  byte size = sizeof(buffer);
  
  // Autenticação
  status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, trailerBlock, &key, &(mfrc522.uid));
  if (status != MFRC522::STATUS_OK) {
    Serial.print(F("Autenticação falhou: "));
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  }
  
  // Leitura e exibição no LCD
  status = mfrc522.MIFARE_Read(blocoAddr, buffer, &size);
  if (status == MFRC522::STATUS_OK) {
    lcd.clear();
    lcd.setCursor(0, 0);
    for (byte i = 1; i < 16; i++) {
      Serial.print(char(buffer[i]));
      lcd.write(char(buffer[i]));
    }
    Serial.println();
  } else {
    Serial.print(F("Leitura falhou: "));
    Serial.println(mfrc522.GetStatusCodeName(status));
  }
  
  // Autenticação e leitura do setor 0, bloco 1
  setor = 0;
  blocoAddr = 1;
  trailerBlock = 3;
  
  // Autenticação
  status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, trailerBlock, &key, &(mfrc522.uid));
  if (status != MFRC522::STATUS_OK) {
    Serial.print(F("Autenticação falhou: "));
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  }
  
  // Leitura e exibição no LCD
  status = mfrc522.MIFARE_Read(blocoAddr, buffer, &size);
  if (status == MFRC522::STATUS_OK) {
    lcd.setCursor(0, 1);
    for (byte i = 0; i < 16; i++) {
      Serial.print(char(buffer[i]));
      lcd.write(char(buffer[i]));
    }
    Serial.println();
  } else {
    Serial.print(F("Leitura falhou: "));
    Serial.println(mfrc522.GetStatusCodeName(status));
  }
  
  // Finaliza o processo e aguarda 3 segundos
  mfrc522.PICC_HaltA();
  mfrc522.PCD_StopCrypto1();
  delay(3000);
  mensagemInicial();
}
